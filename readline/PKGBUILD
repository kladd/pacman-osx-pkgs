# readline

pkgname=readline
_basever=6.3
_patchlevel=006
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('x86_64')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('ncurses')
options=('!emptydirs')
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p))
    done
fi

build() {
  cd ${srcdir}/${pkgname}-$_basever
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # build with -fPIC for x86_64 (FS#15634)
  [ $CARCH == "x86_64" ] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=/usr/local
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${srcdir}/${pkgname}-$_basever
  make DESTDIR=${pkgdir} install
}
md5sums=('33c8fb279e981274f485fd91da77e94a'
         '4343f5ea9b0f42447f102fb61576b398'
         '700295212f7e2978577feaee584afddb'
         'af4963862f5156fbf9111c2c6fa86ed7'
         '11f9def89803a5052db3ba72394ce14f'
         '93721c31cd225393f80cb3aadb165544'
         '71dc6ecce66d1489b96595f55d142a52')
