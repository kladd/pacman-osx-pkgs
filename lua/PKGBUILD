# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua
pkgver=5.2.2
pkgrel=1
pkgdesc='Powerful light-weight programming language designed for extending applications'
arch=('i686' 'x86_64')
url='http://www.lua.org/'
depends=('readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=("http://www.lua.org/ftp/$pkgname-$pkgver.tar.gz" 'lua.pc' 'LICENSE')

build() {
  cd $pkgname-$pkgver
  make macosx
  sed "s/%VER%/${pkgver%.*}/g;s/%REL%/$pkgver/g" ../lua.pc > lua.pc
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver
  make \
    INSTALL_DATA="cp" \
    INSTALL_TOP="$pkgdir/usr/local" \
    INSTALL_MAN="$pkgdir/usr/local/share/man/man1" \
    install
  mkdir -p "$pkgdir/usr/local/lib/pkgconfig"
  install -m644 lua.pc "$pkgdir/usr/local/lib/pkgconfig/lua.pc"
  # Install the documentation
  install -d "$pkgdir/usr/local/share/doc/lua"
  install -m644 doc/*.{gif,png,css,html} "$pkgdir/usr/local/share/doc/lua"

  mkdir -p "$pkgdir/usr/local/share/licenses/$pkgname"
  install -m644 "$srcdir/LICENSE" "$pkgdir/usr/local/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=4 sw=4 et:
md5sums=('efbb645e897eae37cad4344ce8b0a614'
         'b6492c223170149ce17925901883e1e5'
         '0e2bd67b909b9ff673da844ca3480df2')
